* My Field Effect Transistors

* Alpha and Omega AO3400 N-Channel 30V 5.8A Trench MOSFET
* http://www.aosmd.com/pdfs/datasheet/ao3400.pdf
* (C) Alpha and Omega Semi
*              Drain
*              | Gate
*              | | Source
*              | | |
.SUBCKT AO3400 D G S

M1 3 G S S NMOS W=998956u L=1.0u
M2 S G S D PMOS W=998956u L=0.4u
R1 D 3     RTEMP 15E-3
CGS G S    225E-12
DBD S 3    DBD

.MODEL NMOS NMOS (LEVEL=3 TOX=2.5E-8 RS=2E-4 RD=0 NSUB=1.6E17 kp=3E-5 UO=600 THETA=0 VMAX=0 XJ=4E-7 KAPPA=1.1 ETA=0 TPG=1 IS=0 LD=0 CGSO=0 CGDO=0 CGBO=0  NFS=2E10 DELTA=0.1)
.MODEL PMOS PMOS (LEVEL=3 TOX=2.5E-8 NSUB=2.0E16 TPG=-1)
.MODEL DBD D (CJO=690E-12 VJ=0.6 M=0.3 RS=0.005 FC=0.5 IS=1E-11 TT=1.5E-8 N=1.0 BV=36 IBV=1E-4)
.MODEL RTEMP RES (TC1=6.5E-3 TC2=1E-6)

.ENDS

* Alpha and Omega AO3401 P-Channel -30V -4.0A Trench MOSFET
* http://www.aosmd.com/pdfs/datasheet/ao3401.pdf
* (C) Alpha and Omega Semi
*              Drain
*              | Gate
*              | | Source
*              | | |
.SUBCKT AO3401 D G S

M1 3 5 8 8 PMOS W=998956u L=1u
M2 8 5 8 3 NMOS W=998956u L=0.5u
R1 7 3     RTEMP 25E-3
LD D 7     0.5E-9
LS 8 S     1E-9
RG G 6     6
LG 6 5     3E-9
CGS 5 S    1E-12
DBD 3 8    DBD

.MODEL PMOS PMOS (LEVEL=3 TOX=2.5E-8 RS=2E-4 RD=0 NSUB=2E17 UO=120 THETA=0.2 VTO=-1.2 VMAX=5e6 XJ=4E-7 KAPPA=1.1 ETA=0 TPG=1 IS=0 LD=0 CGSO=0 CGDO=0 CGBO=0 NFS=0 DELTA=0)
.MODEL NMOS NMOS (LEVEL=3 TOX=2.5E-8 NSUB=4E16 VTO=1.1 TPG=-1)
.MODEL DBD D (CJO=147E-12 VJ=0.6 M=0.5 RS=0.0 FC=0.5 IS=1E-11 TT=1e-8 N=1.0 BV=36 IBV=1E-4)
.MODEL RTEMP RES (TC1=3.0E-3 TC2=0)

.ENDS

* Infineon IRF3205 55V Single N-Channel Power MOSFET in a TO-220 package
* https://www.infineon.com/cms/en/product/power/mosfet/n-channel/irf3205/
* (C) Infineon
*               Drain
*               | Gate
*               | | Source
*               | | |
.SUBCKT IRF3205 1 2 3
**************************************
*      Model Generated by MODPEX     *
*Copyright(c) Symmetry Design Systems*
*         All Rights Reserved        *
*    UNPUBLISHED LICENSED SOFTWARE   *
*   Contains Proprietary Information *
*      Which is The Property of      *
*     SYMMETRY OR ITS LICENSORS      *
*Commercial Use or Resale Restricted *
*   by Symmetry License Agreement    *
**************************************
* Model generated on Apr 25, 01
* MODEL FORMAT: SPICE3
* Symmetry POWER MOS Model (Version 1.0)
* External Node Designations
* Node 1 -> Drain
* Node 2 -> Gate
* Node 3 -> Source
M1 9 7 8 8 MM L=100u W=100u
.MODEL MM NMOS LEVEL=1 IS=1e-32 VTO=3.73234 LAMBDA=0 KP=95.6501 CGSO=3.64213e-05 CGDO=3.70824e-07
RS 8 3 0.00550387
D1 3 1 MD
.MODEL MD D IS=3.83979e-08 RS=0.00445428 N=1.48671 BV=55 IBV=0.00025 EG=1 XTI=2.99617 TT=0.0001 CJO=3.67939e-09 VJ=1.02405 M=0.469188 FC=0.5
RDS 3 1 2.2e+06
RD 9 1 0.0001
RG 2 7 3.97088
D2 4 5 MD1
* Default values used in MD1:
*   RS=0 EG=1.11 XTI=3.0 TT=0
*   BV=infinite IBV=1mA
.MODEL MD1 D IS=1e-32 N=50 CJO=4.94117e-09 VJ=0.598326 M=0.650772 FC=1e-08
D3 0 5 MD2
* Default values used in MD2:
*   EG=1.11 XTI=3.0 TT=0 CJO=0
*   BV=infinite IBV=1mA
.MODEL MD2 D IS=1e-10 N=0.433451 RS=3e-06
RL 5 10 1
FI2 7 9 VFI2 -1
VFI2 4 0 0
EV16 10 0 9 7 1
CAP 11 10 4.94117e-09
FI1 7 9 VFI1 -1
VFI1 11 6 0
RCAP 6 10 1
D4 0 6 MD3
* Default values used in MD3:
*   EG=1.11 XTI=3.0 TT=0 CJO=0
*   RS=0 BV=infinite IBV=1mA
.MODEL MD3 D IS=1e-10 N=0.433451
.ENDS
