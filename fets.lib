* My Field Effect Transistors

* Alpha and Omega AO3400 N-Channel 30V 5.8A Trench MOSFET
* http://www.aosmd.com/pdfs/datasheet/ao3400.pdf
* (C) Alpha and Omega Semi
*              Drain
*              | Gate
*              | | Source
*              | | |
.SUBCKT AO3400 D G S

M1 3 G S S NMOS W=998956u L=1.0u
M2 S G S D PMOS W=998956u L=0.4u
R1 D 3     RTEMP 15E-3
CGS G S    225E-12
DBD S 3    DBD

.MODEL NMOS NMOS (LEVEL=3 TOX=2.5E-8 RS=2E-4 RD=0 NSUB=1.6E17 kp=3E-5 UO=600 THETA=0 VMAX=0 XJ=4E-7 KAPPA=1.1 ETA=0 TPG=1 IS=0 LD=0 CGSO=0 CGDO=0 CGBO=0  NFS=2E10 DELTA=0.1)
.MODEL PMOS PMOS (LEVEL=3 TOX=2.5E-8 NSUB=2.0E16 TPG=-1)
.MODEL DBD D (CJO=690E-12 VJ=0.6 M=0.3 RS=0.005 FC=0.5 IS=1E-11 TT=1.5E-8 N=1.0 BV=36 IBV=1E-4)
.MODEL RTEMP RES (TC1=6.5E-3 TC2=1E-6)

.ENDS

* Alpha and Omega AO3401 P-Channel -30V -4.0A Trench MOSFET
* http://www.aosmd.com/pdfs/datasheet/ao3401.pdf
* (C) Alpha and Omega Semi
*              Drain
*              | Gate
*              | | Source
*              | | |
.SUBCKT AO3401 D G S

M1 3 5 8 8 PMOS W=998956u L=1u
M2 8 5 8 3 NMOS W=998956u L=0.5u
R1 7 3     RTEMP 25E-3
LD D 7     0.5E-9
LS 8 S     1E-9
RG G 6     6
LG 6 5     3E-9
CGS 5 S    1E-12
DBD 3 8    DBD

.MODEL PMOS PMOS (LEVEL=3 TOX=2.5E-8 RS=2E-4 RD=0 NSUB=2E17 UO=120 THETA=0.2 VTO=-1.2 VMAX=5e6 XJ=4E-7 KAPPA=1.1 ETA=0 TPG=1 IS=0 LD=0 CGSO=0 CGDO=0 CGBO=0 NFS=0 DELTA=0)
.MODEL NMOS NMOS (LEVEL=3 TOX=2.5E-8 NSUB=4E16 VTO=1.1 TPG=-1)
.MODEL DBD D (CJO=147E-12 VJ=0.6 M=0.5 RS=0.0 FC=0.5 IS=1E-11 TT=1e-8 N=1.0 BV=36 IBV=1E-4)
.MODEL RTEMP RES (TC1=3.0E-3 TC2=0)

.ENDS
